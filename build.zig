//! Build script for pyexshalos C/C++ extension modules using Zig.
//!
//! Produces Python-importable shared libraries (.so) for:
//!   spectrum, exshalos, hod, analytical, finder
//!
//! Usage:
//!   zig build                     # builds all modules into zig-out/pyexshalos/lib/
//!   zig build -Ddouble-precision  # use fftw3/fftw3_omp instead of fftw3f/fftw3f_omp

const std = @import("std");

// ─── Per-module configuration ────────────────────────────────────────────

// Structute with the information about a vendored dependency
// It should be used when uting small dependencies
const SourceDep = struct {
    name: []const u8, // key in build.zig.zon
    files: []const []const u8, // paths relative to the dependency root
    includes: []const []const u8 = &.{}, // include subfolders within the dependency
    flags: []const []const u8 = &.{},
};

// Structure for the C/C++ modules
const ExtModule = struct {
    name: []const u8,
    src_dir: []const u8,
    files: []const []const u8,
    libs: []const []const u8,
    cpp: bool = false,
    deps: []const SourceDep = &.{},
};

// List of the C/C++ modules
const modules = [_]ExtModule{
    .{
        .name = "spectrum",
        .src_dir = "src/spectrum",
        .files = &.{
            "spectrum_h.c", "abundance.c", "gridmodule.c", "powermodule.c",
            "bimodule.c",   "trimodule.c", "bias.c",       "spectrum.c",
        },
        .libs = &.{ "m", "gsl", "gslcblas" },
    },
    .{
        .name = "exshalos",
        .src_dir = "src/exshalos",
        .files = &.{
            "fftlog.c",     "exshalos_h.c",       "density_grid.c",
            "find_halos.c", "cells_in_spheres.c", "lpt.c",
            "box.c",        "exshalos.c",
        },
        .libs = &.{ "m", "fftw3", "gsl", "gslcblas" },
    },
    .{
        .name = "hod",
        .src_dir = "src/hod",
        .files = &.{ "hod_h.c", "populate_halos.c", "split_galaxies.c", "hod.c" },
        .libs = &.{ "m", "gsl", "gslcblas" },
    },
    .{
        .name = "analytical",
        .src_dir = "src/analytical",
        .files = &.{ "fftlog.c", "analytical_h.c", "clpt.c", "analytical.c" },
        .libs = &.{ "m", "fftw3", "gsl", "gslcblas" },
    },
    .{
        .name = "halovoid",
        .src_dir = "src/halovoid",
        .files = &.{ "finder.cpp",  "halovoid.cpp" },
        .libs = &.{"m"},
        .cpp = true,
        .deps = &.{
            .{
                .name = "voro",
                .files = &.{
                    "src/cell.cc",       "src/common.cc",
                    "2d/src/cell_2d.cc",
                },
                .includes = &.{ "src", "2d/src" },
                .flags = &.{ "-std=c++23", "-O2", "-funroll-loops", "-fopenmp" }
            },
            .{
                .name = "pdqsort",
                .files = &.{},
                .includes = &.{"./"},
                .flags = &.{ "-std=c++23", "-O2", "-funroll-loops", "-fopenmp" }

            },
        },
    },
};

// ─── Environment detection ─────────────────────────────────────────────────

// Structure for the information about the env
const EnvInfo = struct {
    py_include: []const u8,
    numpy_include: []const u8,
    ext_suffix: []const u8,
    lib_paths: []const []const u8,
    gomp_dir: []const u8,
    gcc_include: []const u8,
};

// Run a bash command and capture its outputs
fn runCapture(b: *std.Build, argv: []const []const u8) ![]const u8 {
    const result = std.process.run(b.graph.arena, b.graph.io, .{
        .argv = argv,
        .environ_map = &b.graph.environ_map,
    }) catch return error.RunFailed;
    return std.mem.trim(u8, result.stdout, " \r\n");
}

// Collect information about the current env
fn detectEnv(b: *std.Build) !EnvInfo {
    // Python + numpy in one subprocess
    const py_script =
        \\import sysconfig, numpy
        \\print(sysconfig.get_path("include"))
        \\print(sysconfig.get_config_var("EXT_SUFFIX"))
        \\print(numpy.get_include())
    ;
    const py_out = blk: {
        const r = std.process.run(b.graph.arena, b.graph.io, .{
            .argv = &.{ "python3", "-c", py_script },
            .environ_map = &b.graph.environ_map,
        }) catch break :blk null;
        break :blk r.stdout;
    } orelse {
        std.debug.print("error: could not run 'python3'. Run inside `nix develop` or activate .venv.\n", .{});
        return error.PythonNotFound;
    };

    // Parse the informations about Python + numpy
    var it = std.mem.splitScalar(u8, py_out, '\n');
    const py_include = std.mem.trim(u8, it.next() orelse "", " \r");
    const ext_suffix = std.mem.trim(u8, it.next() orelse "", " \r");
    const numpy_include = std.mem.trim(u8, it.next() orelse "", " \r");
    if (py_include.len == 0 or ext_suffix.len == 0 or numpy_include.len == 0) {
        std.debug.print("error: unexpected python3 output: {s}\n", .{py_out});
        return error.PythonOutputInvalid;
    }

    // Parse $LIBRARY_PATH
    var lib_paths: []const []const u8 = &.{};
    if (b.graph.environ_map.get("LIBRARY_PATH")) |lp| {
        var count: usize = 0;
        var splitter = std.mem.splitScalar(u8, lp, ':');
        while (splitter.next()) |part| {
            if (part.len > 0) count += 1;
        }
        const arr = b.graph.arena.alloc([]const u8, count) catch @panic("OOM");
        var i: usize = 0;
        splitter = std.mem.splitScalar(u8, lp, ':');
        while (splitter.next()) |part| {
            if (part.len > 0) {
                arr[i] = part;
                i += 1;
            }
        }
        lib_paths = arr;
    }

    // Find libgomp via the C compiler
    var gomp_dir: []const u8 = "";
    if (runCapture(b, &.{ "gcc", "-print-file-name=libgomp.so" })) |path| {
        // gcc returns full path like /nix/store/.../lib/libgomp.so — extract dir
        if (std.fs.path.dirname(path)) |dir| {
            if (dir.len > 0 and !std.mem.eql(u8, dir, ".")) gomp_dir = dir;
        }
    } else |_| {}

    // GCC's private include dir (omp.h, etc.)
    var gcc_include: []const u8 = "";
    if (runCapture(b, &.{ "gcc", "-print-file-name=include" })) |path| {
        if (path.len > 0 and !std.mem.eql(u8, path, "include")) gcc_include = path;
    } else |_| {}

    return .{
        .py_include = py_include,
        .numpy_include = numpy_include,
        .ext_suffix = ext_suffix,
        .lib_paths = lib_paths,
        .gomp_dir = gomp_dir,
        .gcc_include = gcc_include,
    };
}

// ─── Build ─────────────────────────────────────────────────────────────────

pub fn build(b: *std.Build) void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const double_precision = b.option(
        bool,
        "double-precision",
        "Use double-precision FFTW3 (fftw3/fftw3_omp) instead of float (fftw3f/fftw3f_omp)",
    ) orelse false;

    const env = detectEnv(b) catch @panic("Environment detection failed");

    const fftw_libs: []const []const u8 = if (double_precision)
        &.{ "fftw3", "fftw3_omp" }
    else
        &.{ "fftw3f", "fftw3f_omp" };

    const c_flags: []const []const u8 = &.{ "-O2", "-funroll-loops", "-fopenmp" };
    const cpp_flags: []const []const u8 = &.{ "-O2", "-funroll-loops", "-fopenmp", "-std=c++23" };

    for (&modules) |ext| {
        // 1 — Create module
        const mod = b.createModule(.{
            .target = target,
            .optimize = optimize,
            .link_libc = true,
            .link_libcpp = if (ext.cpp) true else null,
            .pic = true,
        });

        // 2 — C/C++ sources
        mod.addCSourceFiles(.{
            .root = b.path(ext.src_dir),
            .files = ext.files,
            .flags = if (ext.cpp) cpp_flags else c_flags,
            .language = if (ext.cpp) .cpp else .c,
        });

        // 3 — Include dirs: Python, numpy, project headers, gcc (omp.h)
        mod.addSystemIncludePath(.{ .cwd_relative = env.py_include });
        mod.addSystemIncludePath(.{ .cwd_relative = env.numpy_include });
        if (env.gcc_include.len > 0) mod.addSystemIncludePath(.{ .cwd_relative = env.gcc_include });

        // --- MACOS FIX: Add Homebrew paths (fftw/gsl install directly here;
        //     libomp header + libomp/libgomp dylibs are symlinked here by the CI step) ---
        if (target.result.os.tag == .macos) {
            mod.addSystemIncludePath(.{ .cwd_relative = "/opt/homebrew/include" });
            mod.addLibraryPath(.{ .cwd_relative = "/opt/homebrew/lib" });
        }

        // ----------------------------------------------------

        // Add the external dependencies
        for (ext.deps) |dep| {
            const d = b.dependency(dep.name, .{ .target = target, .optimize = optimize });
            for (dep.includes) |inc| mod.addIncludePath(d.path(inc));
            mod.addCSourceFiles(.{
                .root = d.path(""),
                .files = dep.files,
                .flags = dep.flags,
            });
        }

        // 4 — Library search paths (from $LIBRARY_PATH + gcc libgomp dir)
        for (env.lib_paths) |p| mod.addLibraryPath(.{ .cwd_relative = p });
        if (env.gomp_dir.len > 0) mod.addLibraryPath(.{ .cwd_relative = env.gomp_dir });

        // ----------------------------------------------------

        // 5 — Libraries (.needed = true forces DT_NEEDED even if only an
        //     indirect dep needs it — e.g. libgsl needs cblas_* from gslcblas)
        for (ext.libs) |lib| {
            mod.linkSystemLibrary(lib, .{ .needed = true });
        }
        for (fftw_libs) |lib| {
            mod.linkSystemLibrary(lib, .{ .needed = true });
        }
        // OpenMP runtime: must link libomp (LLVM __kmpc_* API), NOT libgomp
        // (GNU GOMP_* API). Zig's clang generates __kmpc_* calls under -fopenmp.
        // libgomp is still pulled in transitively by libfftw3_*_omp (GCC-built).
        mod.linkSystemLibrary("omp", .{ .needed = true });

        if (double_precision) {
            mod.addCMacro("DOUBLEPRECISION_FFTW", "");
        }

        // 6 — Shared library
        const lib = b.addLibrary(.{
            .name = ext.name,
            .root_module = mod,
            .linkage = .dynamic,
        });

        if (target.result.os.tag == .macos) {
            lib.linker_allow_shlib_undefined = true;
        }

        // -----------------------------------------------------------

        // 7 — Install with Python extension suffix
        const dest = b.fmt("lib/{s}{s}", .{ ext.name, env.ext_suffix });
        const install = b.addInstallFile(lib.getEmittedBin(), dest);
        install.step.dependOn(&lib.step);
        b.getInstallStep().dependOn(&install.step);
    }
}
